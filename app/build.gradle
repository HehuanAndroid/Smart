import java.text.SimpleDateFormat

plugins {
    id 'com.android.application'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    buildFeatures.dataBinding = true

    defaultConfig {
        applicationId "com.vcom.smart"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 104
        //versionName "1.0.4"
        //versionName "1.0.5"
        versionName "1.0.6"
        //1.修改新增场景后，没有添加设备的情况下不能点击保存
        //2.更新 设备调控灯界面，调整颜色后再调整亮度保存当前的颜色值
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    def keystoreFilepath = ''
    def keystorePSW = ''
    def keystoreAlias = ''
    def keystoreAliasPSW = ''
    def keyfile = file('s.keystore.temp')

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    keystoreFilepath = properties.getProperty("keystore.path")

    if (keystoreFilepath) {
        keystorePSW = properties.getProperty("keystore.password")
        keystoreAlias = properties.getProperty("keystore.alias")
        keystoreAliasPSW = properties.getProperty("keystore.alias_password")
        keyfile = file(keystoreFilepath)
    }

    signingConfigs {
        release {
            keyAlias keystoreAlias
            keyPassword keystoreAliasPSW
            storeFile keyfile
            storePassword keystorePSW
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            if (keyfile.exists()) {
                println("WITH -> buildTypes -> release: using jks key")
                signingConfig signingConfigs.release
            } else {
                println("WITH -> buildTypes -> release: using default key")
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }

    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                def df = new SimpleDateFormat("yyyy-MM-dd_HH-mm")
                outputFileName = "VcomSmart_competition_v${defaultConfig.versionName}_${df.format(new Date())}.apk"
            }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    implementation 'com.google.android.material:material:1.4.0'
    implementation 'com.google.code.gson:gson:2.8.7'

    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.9.0'
    implementation 'org.conscrypt:conscrypt-openjdk-uber:1.1.3'
    
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'

    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'

    implementation 'androidx.room:room-runtime:2.3.0-alpha03'
    annotationProcessor 'androidx.room:room-compiler:2.3.0-alpha03'

    implementation 'org.greenrobot:eventbus:3.2.0'

    implementation 'cat.ereza:customactivityoncrash:2.3.0'

   /* implementation "com.umeng.umsdk:common:9.3.0"
    implementation "com.umeng.umsdk:asms:1.1.3"
    implementation "com.umeng.umsdk:crash:0.0.5"*/

    implementation project(':BluetoothLightLib')
    implementation 'com.github.DylanCaiCoding:ActivityResultLauncher:1.1.2'
    /** 下拉刷新 */
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
}